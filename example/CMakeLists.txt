# Common examples (valid for all platforms)
FILE(GLOB EXAMPLE_SOURCES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ./example_*.c)
FOREACH(EXAMPLE_SOURCE ${EXAMPLE_SOURCES})
    # Rule to build example
    GET_FILENAME_COMPONENT(EXAMPLE_NAME ${EXAMPLE_SOURCE} NAME_WE)
    ADD_EXECUTABLE(${EXAMPLE_NAME} ${EXAMPLE_SOURCE} ${PROJECT_PORT_ISR_SOURCES}) # TODO quitar ISR
    IF(DEFINED PLATFORM_EXTENSION)
        SET_TARGET_PROPERTIES(${EXAMPLE_NAME} PROPERTIES SUFFIX ${PLATFORM_EXTENSION})
    ENDIF()
    IF(PROJECT_COMMON_SOURCES)
        TARGET_LINK_LIBRARIES(${EXAMPLE_NAME} ${PROJECT_NAME}-common)
    ENDIF()
    TARGET_LINK_LIBRARIES(${EXAMPLE_NAME} ${PROJECT_NAME}-port)
    IF(USE_FSM)
        TARGET_LINK_LIBRARIES(${EXAMPLE_NAME} fsm)
    ENDIF()

    IF(DEFINED OPENOCD_CONFIG_FILE)
        ADD_CUSTOM_TARGET(flash-${EXAMPLE_NAME}
            DEPENDS ${EXAMPLE_NAME}
            COMMAND ${OPENOCD_EXECUTABLE} -f ${OPENOCD_CONFIG_FILE} -c "program ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${EXAMPLE_NAME}${PLATFORM_EXTENSION} verify reset exit"
            COMMENT "Flashing ${EXAMPLE_NAME}")
    ENDIF()
    IF(DEFINED QEMU_FLAGS)
        ADD_CUSTOM_TARGET(emulate-${EXAMPLE_NAME}
            DEPENDS ${EXAMPLE_NAME}
            COMMAND ${QEMU_EXECUTABLE} ${QEMU_FLAGS} -kernel ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${EXAMPLE_NAME}${PLATFORM_EXTENSION}
            COMMENT "Emulating ${EXAMPLE_NAME}")
    ENDIF()
ENDFOREACH(EXAMPLE_SOURCE)

# Search additional platform-specific examples (only valid for a specific platform)
FILE(GLOB children RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/*)
FOREACH (child ${children})
    IF(IS_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${child})
        # assert that PLATFORM starts with the name of child directory
        STRING(FIND ${PLATFORM} ${child} PLATFORM_STARTS_WITH)
        IF(PLATFORM_STARTS_WITH EQUAL 0)
            # add example subdirectory if it exists
            ADD_SUBDIRECTORY(${child})
        ENDIF()
    ENDIF()
ENDFOREACH(child)
